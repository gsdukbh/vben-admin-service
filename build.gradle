plugins {
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.11.3'
    id 'com.google.cloud.tools.jib' version '3.5.1'
    id 'java'
}

group = 'top.werls'
version = '0.0.1-SNAPSHOT'
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    // 使用阿里云开源镜像代理 https://developer.aliyun.com/mvn/guide
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
    maven {
        url 'https://maven.aliyun.com/repository/spring/'
    }
    maven { url 'https://maven.aliyun.com/repository/public/' }
    maven {
        url 'https://maven.aliyun.com/repository/spring-plugin'
    }
    mavenLocal()
    mavenCentral()
}

jib{
    // Google jib plugin for building Docker images
    from {
//        image = 'amazoncorretto:21'
        image = 'gcr.io/distroless/java21-debian12'
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }

    to {
        // todo change to your docker repository
        image = "gsdukbh/${project.name.toLowerCase()}"
        tags = ['latest', version]
        auth {
            username = System.getenv('DOCKER_USERNAME') ?: 'gsdukbh'
            // 从本地文件获取 from file
            def p = ""
            try {
                p = new File('./DOCKER_PASSWORD').text
            } catch (Exception e) {
                println "DOCKER_PASSWORD file not found, using environment variable instead."
            }
            password = System.getenv('DOCKER_PASSWORD') ?: p
        }
    }

    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        ports = ['8080'] //todo Expose port 8080
        volumes = ['/app/log','/app/db.sqlite']
        workingDirectory= '/app'
        environment = [JAVA_TOOL_OPTIONS: '-XX:+UseContainerSupport -XX:MaxRAMPercentage=80.0']
    }

}


dependencies {

    def gsonVersion = '2.13.2'
    def lang3Version = '3.20.0'
    def springdocVersion = '2.8.14'
    def jwtVersion = '0.13.0'
    def flywaydbVersion = '11.17.1'
    def mysqlVersion = '9.5.0'
    def postgresql='42.7.8'

    runtimeOnly 'org.springframework.boot:spring-boot-docker-compose'

    // mapstruct https://mapstruct.org/
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //sql 版本控制 https://flywaydb.org/
    implementation "org.flywaydb:flyway-core:${flywaydbVersion}"
    implementation "org.flywaydb:flyway-mysql:${flywaydbVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywaydbVersion}"


    //mysql
    runtimeOnly "com.mysql:mysql-connector-j:${mysqlVersion}"
    runtimeOnly "org.postgresql:postgresql:${postgresql}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "org.apache.commons:commons-lang3:${lang3Version}"

    // spring doc https://springdoc.org/
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    // jwt https://github.com/jwtk/jjwt
    implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
    // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
    implementation "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
    implementation "io.jsonwebtoken:jjwt-gson:${jwtVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'rg.springframework.boot:spring-boot-starter-aspectj'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}
